buildscript {
	repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

repositories {
	mavenCentral()
}

apply plugin: 'gwt'
apply plugin: 'java'

gwt {
	gwtVersion '2.7.0'
	modules 'org.scilab.forge.JLaTeXMathGWT'
}


dependencies {
	compile project(':jlatexmath-share')
	compile files(project(':jlatexmath-share').sourceSets.main.java.srcDirs)
	compile 'com.google.gwt:gwt-dev:2.7.0'
	compile 'com.google.gwt:gwt-user:2.7.0'
	testCompile 'com.google.gwt:gwt-user:2.7.0'
	testCompile 'junit:junit:4.12'
}

task runSuperDev(type: JavaExec, dependsOn: 'jar') {
	description 'Starts a codeserver, and a simple webserver for development'
	main 'com.google.gwt.dev.DevMode'
	maxHeapSize '2048m'
	classpath ((configurations.compile as Set<String>) 
		+ (sourceSets.main.allSource.srcDirs as Set<String>) 
		+ (jar.archivePath as String))
	workingDir file('war')
	args '-logLevel', 'INFO',
		'-codeServerPort', '9997',
		'-port', '8888',
		'-superDevMode',
		'-war', file('war'),
		'org.scilab.forge.JLaTeXMathGWTDev'
	systemProperty 'gwt.persistentunitcachedir', '../build/gwt/devModeCache'
}
task jarAll(type: Jar) {
	classifier = 'all'
	from project.sourceSets.main.java
	from project.sourceSets.main.output
	from project(':jlatexmath-share').sourceSets.main.java
	from project(':jlatexmath-share').sourceSets.main.output
}

task jarSources(type: Jar, dependsOn: classes) {
	classifier = 'sources'
    from sourceSets.main.allSource 
}

// exclude from the eclipse classpath the java files in the resources dir
eclipse.classpath.file { 
	whenMerged { classpath ->
		classpath.entries.find { entry -> entry.kind == 'src' && entry.path == 'src/main/resources' }.excludes = ['**/*.java']
	}
}